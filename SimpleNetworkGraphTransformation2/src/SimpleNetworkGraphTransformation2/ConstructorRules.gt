import "http://www.eclipse.org/emf/2002/Ecore"
import "platform:/resource/SimpleNetwork/model/SimpleNetwork.ecore"

rule createNetwork {
	++ network : Network
}

rule generateDevice{
	network: Network{
		.deviceNumber := network.deviceNumber -1
		.maxBandwidth := network.maxBandwidth - network.maxBandwidth/network.deviceNumber
	}
	# network.deviceNumber > 0
	
	++ device: Device{
		.maxBandwidth := network.maxBandwidth/network.deviceNumber
		.flag := network.maxBandwidth%6
		.maxConnections := ln(network.maxBandwidth)
	}
}

rule generateComputer refines generateDevice{
	++ device: Computer
}

rule generateServer refines generateDevice{
	++ device: Server
}

rule insertDevicesIntoNetwork() {
	network : Network {
		++ -devices->device
	}
	  
	device : Device
} when deviceNotInNetwork

pattern findNetwork{
	network : Network
}

condition deviceNotInNetwork = forbid findDevice
condition computerNotInNetwork = forbid findComputer
condition serverNotInNetwork = forbid findServer

pattern findDevice{
	network : Network {
		-devices->device
	}
	
	device : Device
}

pattern findComputer refines findDevice{
	device : Computer
}

pattern findServer refines findDevice{
	device : Server
}