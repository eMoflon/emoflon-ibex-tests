#using MoDiscoIbexTGG.*
#using AttrCondDefLibrary.*

#rule JavaModelToUMLModel #with MoDiscoIbexTGG

#source { 
	++ jp : Model
}

#target {

	++ up : Model
}

#correspondence {

	++ jp2up : JavaModel2UMLPackage{
		#src->jp
		#trg->up
	}
}

#attributeConditions {
	eq_string(up.name,jp.name)
}

#rule JavaPackageToUMLPackage #with MoDiscoIbexTGG

#source { 
	++ jp : Package
}

#target {

	++ up : Package
}

#correspondence {

	++ jp2up : JavaPackage2UmlPackage{
		#src->jp
		#trg->up
	}
}

#attributeConditions {
	eq_string(up.name,jp.name)
}


#rule JavaPackageToUMLPackage_EdgeRule #with MoDiscoIbexTGG

#source { 
	jpp : Package{
		++-ownedPackages->jp
	}
    jp : Package
}

#target {
	
	upp : Package{
		++-packagedElement->up
	}
	up : Package
}

#correspondence {

	jp2up : JavaPackage2UmlPackage{
		#src->jp
		#trg->up
	}
	jpp2upp : JavaPackage2UmlPackage{
		#src->jpp
		#trg->upp
	}
}

#rule JavaModelToUMLModel_EdgeRule #with MoDiscoIbexTGG

#source { 
	jpp : Model{
		++-ownedElements->jp
	}
    jp : Package
}

#target {
	
	upp : Model{
		++-packagedElement->up
	}
	up : Package
}

#correspondence {

	jp2up : JavaPackage2UmlPackage{
		#src->jp
		#trg->up
	}
	jpp2upp : JavaModel2UMLPackage{
		#src->jpp
		#trg->upp
	}
}

