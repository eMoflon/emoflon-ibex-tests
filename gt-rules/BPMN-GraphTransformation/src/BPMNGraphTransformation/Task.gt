import "http://www.eclipse.org/emf/2002/Ecore"
import "platform:/resource/BPMN/model/BPMN.ecore"

/**
 * Finds a task.
 */
pattern findTask() {
	task: Task
}

/**
 * Finds a task with the given name.
 */
pattern findTaskByName(name: EString) {
	task: Task {
		.name == param::name
	}
}

/**
 * Add a task.
 */
rule addTask {
	++ task: Task
}

abstract rule insertTask(name: EString) {
	++ newTask: Task {
		.name := param::name
	}

	++ newFlow: SequenceFlow
}

/**
 * Inserts a task before another task.
 */
rule insertTaskBefore
refines insertTask {
	to: Task {
		++ -incoming -> newFlow
	}

	++ newTask: Task {
		++ -outgoing -> newFlow
	}
}

/**
 * Inserts a task between two other tasks.
 */
rule insertTaskBetween
refines insertTask {
	from: Task {
		-outgoing -> flow
	}

	to: Task {
		-- -incoming -> flow
		++ -incoming -> newFlow
	}

	flow: SequenceFlow

	++ newTask: Task {
		++ -outgoing -> newFlow
		++ -incoming -> flow
	}
}

/**
 * Inserts a task after another task.
 */
rule insertTaskAfter
refines insertTask {
	from: Task {
		++ -outgoing -> newFlow
	}

	++ newTask: Task {
		++ -incoming -> newFlow
	}
}
