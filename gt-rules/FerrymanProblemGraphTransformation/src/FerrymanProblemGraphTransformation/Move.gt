import "platform:/resource/FerrymanProblem/model/FerrymanProblem.ecore"

/**
 * At the end all things must be at the right bank.
 */
rule checkAllThingsAtRightBank {
	model: Model {
		-rightBank -> rightBank
	}

	rightBank: Bank

	wolf: Wolf {
		-isAt -> rightBank
	}

	goat: Goat {
		-isAt -> rightBank
	}

	cabbage: Cabbage {
		-isAt -> rightBank
	}
}

/**
 * The ferryman can move with an empty boat to the other bank.
 */
rule move {
	bankBefore: Bank

	bankAfter: Bank

	ferryMan: FerryMan {
		-- -isAt -> bankBefore
		++ -isAt -> bankAfter
	}
}

/**
 * If the ferryman is on the left bank, he can move one thing to the right.
 * If the ferryman is on the right bank, he can move one thing to the left.
 */
rule moveThing {
	bankBefore: Bank

	bankAfter: Bank

	ferryMan: FerryMan {
		-- -isAt -> bankBefore
		++ -isAt -> bankAfter
	}

	thing: Thing {
		-- -isAt -> bankBefore
		++ -isAt -> bankAfter
	}
}
