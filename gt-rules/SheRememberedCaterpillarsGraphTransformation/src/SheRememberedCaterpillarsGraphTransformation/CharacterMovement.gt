import "platform:/resource/SheRememberedCaterpillars/model/SheRememberedCaterpillars.ecore"

/*
 * Move a character from a platform to another.  
 */
abstract rule moveCharacter {
	character: Character {
		-- -standsOn -> platform1
		++ -standsOn -> platform2
	}

	platform1: Platform

	platform2: Platform
}

/*
 * Move a character from a platform to another.  
 */
rule moveCharacterToNeighboringPlatform
refines moveCharacter {
	platform1: Platform {
		-neighbors -> platform2
	}
}

abstract rule moveCharacterAcrossPlatformConnector
refines moveCharacter {
	platform1: SimplePlatform

	platform2: SimplePlatform
}

/**
 * Move a character from a platform to another crossing a bridge.
 */
rule moveCharacterAcrossBridge
refines moveCharacterAcrossPlatformConnector {
	bridge: Bridge {
		-connects -> platform1
		-connects -> platform2
	}
}

/**
 * Move a character from a platform to another one behind a wall.
 */
rule moveCharacterOverWall
refines moveCharacterAcrossPlatformConnector {
	wall: Wall {
		-connects -> platform1
		-connects -> platform2
	}
}
