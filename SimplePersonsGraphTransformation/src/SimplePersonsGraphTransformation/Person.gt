import "http://www.eclipse.org/emf/2002/Ecore"
import "platform:/resource/SimplePersons/model/SimplePersons.ecore"

/**
 * Create a person.
 */
abstract rule createPerson(name: EString) {
	++ person: Person {
		.name := param::name
	}

	register: PersonRegister {
		++ -persons -> person
	}
}

/**
 * Creates a female.
 */
rule createFemale(name: EString)
refines createPerson {
	++ person: Female
}

/**
 * Creates a person.
 */
rule createMale(name: EString)
refines createPerson {
	++ person: Male
}

/**
 * Finds a person.
 */
pattern findPerson {
	person: Person
}

/**
 * Finds a male.
 */
pattern findMale
refines findPerson {
	person: Male
}

/**
 * Finds a female.
 */
pattern findFemale
refines findPerson {
	person: Female
}

/**
 * Finds the first person when ordering the persons in the register alphabetically.
 */
pattern findFirstPerson
refines findPerson
when noPersonWithSmallerName

condition noPersonWithSmallerName = forbid anotherPersonWithSmallerName

abstract pattern anotherPersonWithSmallerName
refines findPerson {
	anotherPerson: Person
	anotherPerson.name < person.name
}

/**
 * Finds the last person when ordering the persons in the register alphabetically.
 */
pattern findLastPerson
refines findPerson
when noPersonWithGreaterName

condition noPersonWithGreaterName = forbid anotherPersonWithGreaterName

abstract pattern anotherPersonWithGreaterName
refines findPerson {
	person: Person 
	person.name < anotherPerson.name

	anotherPerson: Person
}
