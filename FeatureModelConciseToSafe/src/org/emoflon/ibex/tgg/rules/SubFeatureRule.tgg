#using FeatureModelConciseToSafe.*
#using AttrCondDefLibrary.*

#abstract #rule SubFeatureRule #with FeatureModelConciseToSafe

#source { 
	conciseParent:FeatureModelConcise.Feature {
		-groups->conciseGroup
		++ -subfeatures->conciseChild
	}
	
	conciseGroup:FeatureModelConcise.Group {
		++ -members->conciseChild
	}
	
	++ conciseChild:FeatureModelConcise.Feature
}

#target {
	safeParent:FeatureModelSafe.Feature {
		-groups->safeGroup
	}
	
	safeGroup:FeatureModelSafe.Group {
		++ -members->safeChild
	}
	
	++ safeChild:FeatureModelSafe.Feature
}

#correspondence {
	p2p:Feature2Feature {
		#src->conciseParent
		#trg->safeParent
	}
	
	++ c2c:Feature2Feature {
		#src->conciseChild
		#trg->safeChild
	}
	
	g2g:Group2Group {
		#src->conciseGroup
		#trg->safeGroup
	}
}

#attributeConditions {
	eq_string(conciseChild.name, safeChild.name)
}

#rule XOrRule #extends SubFeatureRule #with FeatureModelConciseToSafe

#source { 
	conciseGroup:FeatureModelConcise.XorGroup 
}

#target {
	safeGroup:FeatureModelSafe.XorGroup
}

#rule OrRule #extends SubFeatureRule #with FeatureModelConciseToSafe

#source { 
	conciseGroup:FeatureModelConcise.OrGroup
}

#target {
	safeGroup:FeatureModelSafe.OrGroup
}
