import "http://www.eclipse.org/emf/2002/Ecore"
import "platform:/resource/BPMN/model/BPMN.ecore"
import "platform:/resource/BPMNInstanceRepresentation/model/BPMNInstanceRepresentation.ecore"

pattern findTaskIR {
	task: TaskIR
}

abstract rule addInactiveTaskIR(name: EString) {
	++ newTask: TaskIR {
		.name := param::name
		++ -lifecycle -> lcnew
	}

	++ lcnew: Lifecycle {
		.activeState := enum::INACTIVE
	}
}

/**
 * Adds an inactive task to between two tasks.The existing tasks must not be closed.
 */
rule addInactiveTaskIRBetweenTasks
refines addInactiveTaskIR {
	from: TaskIR {
		-outgoing -> flow
		-lifecycle -> lcfrom
	}

	lcfrom: Lifecycle {
		.activeState != enum::CLOSED
	}

	to: TaskIR {
		-- -incoming -> flow
		++ -incoming -> newFlow
		-lifecycle -> lcto
	}

	lcto: Lifecycle {
		.activeState != enum::CLOSED
	}

	flow: SequenceFlow

	++ newTask: TaskIR {
		++ -outgoing -> newFlow
		++ -incoming -> flow
	}

	++ newFlow: SequenceFlow

	processIR: ProcessIR {
		++ -flowElements -> newTask
		++ -flowElements -> newFlow
		-flowElements -> from
		-flowElements -> to
		-flowElements -> flow
	}
}
when inactiveTasks

condition inactiveTasks = forbid activeFromTask && forbid activeToTask

pattern activeFromTask {
	from: TaskIR

	processIR: ProcessIR {
		-activeTasks -> from
	}
}

pattern activeToTask {
	to: TaskIR

	processIR: ProcessIR {
		-activeTasks -> to
	}
}
