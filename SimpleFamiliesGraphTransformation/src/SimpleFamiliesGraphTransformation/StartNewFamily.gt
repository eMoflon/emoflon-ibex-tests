import "http://www.eclipse.org/emf/2002/Ecore"
import "platform:/resource/SimpleFamilies/model/SimpleFamilies.ecore"

/**
 * Two family members start a new family.
 */
rule marry(familyName: EString) {
	bride: FamilyMember

	brideFamily: Family {
		-- -daughters -> bride
	}

	groom: FamilyMember

	groomFamily: Family {
		-- -sons -> groom
	}

	register: FamilyRegister {
		++ -families -> newFamily
	}

	++ newFamily: Family {
		.name := param::familyName
		++ -father -> groom
		++ -mother -> bride
	}
}

/**
 * Adds a daughter to the mother's family.
 */
abstract rule childBorn(firstName: EString) {
	mother: FamilyMember

	family: Family {
		-mother -> mother
	}

	++ child: FamilyMember {
		.name := param::firstName
	}
}

/**
 * Adds a daughter to the mother's family.
 */
rule daughterBorn(firstName: EString)
refines childBorn {
	family: Family {
		++ -daughters -> child
	}
}

/**
 * Adds a son to the mother's family.
 */
rule sonBorn(firstName: EString)
refines childBorn {
	family: Family {
		++ -sons -> child
	}
}

/**
 * Adds a son to the mother's family who is named as his father.
 */
rule sonBornNamedAsFather {
	father: FamilyMember

	mother: FamilyMember

	family: Family {
		-father -> father
		-mother -> mother
		++ -sons -> child
	}

	++ child: FamilyMember {
		.name := father.name
	}
}
